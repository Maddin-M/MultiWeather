plugins {
    id "java"
    id "checkstyle"
    id "com.github.johnrengelman.shadow" version "$shadowVersion"
    id "com.github.spotbugs" version "$spotbugsVersion"
    id "name.remal.sonarlint" version "$sonarlintVersion"
}

group "de.maddin.multiweather"
version "1.2.0"

repositories {
    mavenCentral()
    maven {
        url = uri("https://papermc.io/repo/repository/maven-public/")
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:$paperVersion"
    implementation "org.bstats:bstats-bukkit:$bstatsVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

shadowJar {
    dependsOn "test", "check"
    archiveClassifier.set("")
    relocate "org.bstats", "de.maddin.multiweather"
    minimize()
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion "$checkstyleVersion" // https://checkstyle.sourceforge.io/
    configFile file("config/checkstyle/checkstyle.xml")
}

configurations {
    testImplementation.extendsFrom compileOnly
}